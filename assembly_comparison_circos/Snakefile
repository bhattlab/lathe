#!/usr/bin/env python

import os

localrules: faidx, stage_assemblies, assembly_alignment_filter, bwa_index, highlight_alignments, circos

#parse the provided assemblies
condtp_asm = {}

with open(config['assemblies']) as asmf:
	basedir = os.path.dirname(config['assemblies'])
	for l in asmf.readlines():
		s = l.split("\t")
		if len(s) == 1:
			continue

		asm = os.path.join(basedir, s[0])
		cond = s[1]
		tp = s[2].strip()
		condtp = "_".join([cond, tp])
		condtp_asm[condtp] = asm

aligned_filenames = [c for c in condtp_asm]
assembly_files = [condtp_asm[k] for k in condtp_asm]

rule targets:
	input:
		'circos/circos.pdf',
		config['reference'],
		config['dark_contigs'],
		config['highlight_sequences'],
		config['highlight_intensities'],
		config['assemblies']

rule faidx:
	conda:
		"envs/mgwf.yaml"
	input:
		config['reference']
	output:
		"{ref}.fai".format(ref =config['reference'])
	shell:
		"samtools faidx {input}"

#set up a symlink for each input assembly encoded with the timepoint and condition
rule stage_assemblies:
	input:
		#When this file is parsed, the wildcard value is unknown. So in order to index into this dictionary,
		#a lambda function delays retrieval of the value until job dependencies (and wildcard values)
		#are resolved.
		lambda wildcards: condtp_asm[wildcards.f]
	output:
		"assemblies/{f}.fa"
	shell:
		"ln -s {input} {output}"

#Assembly alignments to reference
rule assembly_alignment:
	conda:
		"envs/mgwf.yaml"
	input:
		config['reference'],
		rules.stage_assemblies.output
	output:
		"aligned/{f}.coords"
	shell:
		"nucmer {input[0]} assemblies/{wildcards.f}.fa -p aligned/{wildcards.f}; " +
			"delta-filter -1 aligned/{wildcards.f}.delta > aligned/{wildcards.f}.delta.filt; " +
			"show-coords aligned/{wildcards.f}.delta.filt -T > aligned/{wildcards.f}.coords; "

rule assembly_alignment_filter:
	conda:
		"envs/mgwf.yaml"
	input:
		rules.assembly_alignment.output
	output:
		"aligned/{f}.bed"
	params:
		gap_width = "1000",
	script:
		"scripts/coord_filter.py"

rule bwa_index:
	conda:
		"envs/mgwf.yaml"
	input:
		config['reference']
	output:
		"bwa_idx/ref.fa",
		"bwa_idx/ref.fa.amb",
		"bwa_idx/ref.fa.ann",
		"bwa_idx/ref.fa.bwt",
		"bwa_idx/ref.fa.pac",
		"bwa_idx/ref.fa.sa"
	resources:
		mem=1,
		time=1
	threads: 1
	shell:
		"ln -s {input} bwa_idx/ref.fa; bwa index bwa_idx/ref.fa"


#16S (highlight sequence) alignments
rule highlight_alignments:
	conda:
		"envs/mgwf.yaml"
	input:
		"bwa_idx/ref.fa",
		config['highlight_sequences']
	output:
		"highlighted/highlights.bed"
	shell:
		"bwa mem {input} -a > highlighted/highlights.sam;" +
		"samtools view highlighted/highlights.sam -F4 | cut -f1,3,4 | \
		sort -u -k1,1 | awk '{{print $2, $3, $3 + 1000, $1}}' > {output}" #| cut -f1,3,4 #|awk '{{print $2, $3, $3 + 1000, $1}}' > {output}"
		##grep NM:i:0 | 

rule circos:
	conda:
		"envs/mgwf.yaml"
	input:
		rules.faidx.output,
		config['dark_contigs'],
		rules.highlight_alignments.output,
		config['highlight_intensities'],
		expand("aligned/{f}.bed", f = aligned_filenames)
	output:
		"circos/circos.pdf"
	script:
		"scripts/circos.R"
